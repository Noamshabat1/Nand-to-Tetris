// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

// This chip is an extension of the regular CPU that uses the extended ALU.
// If instruction[15]==0 or (instruction[14]==1 and instruction[13]==1),
// then CpuMul behaves exactly the same as the regular CPU.
// If instruction[15]==1 and instruction[14]==0 the chip will behave as follows:
// | Instruction           | 15 | 14 | 13 | a | c1 | c2 | c3 | c4 | c5 | c6 |
// |-----------------------|:--:|:--:|:--:|:-:|:--:|:--:|:--:|:--:|:--:|:--:|
// | Regular a-instruction |  0 |  * |  * | * |  * |  * |  * |  * |  * |  * |
// | Regular c-instruction |  1 |  1 |  1 | * |  * |  * |  * |  * |  * |  * |
// | dest=A<<;jump         |  1 |  0 |  1 | 0 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=D<<;jump         |  1 |  0 |  1 | 0 |  1 |  1 |  0 |  0 |  0 |  0 |
// | dest=M<<;jump         |  1 |  0 |  1 | 1 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=A>>;jump         |  1 |  0 |  1 | 0 |  0 |  0 |  0 |  0 |  0 |  0 |
// | dest=D>>;jump         |  1 |  0 |  1 | 0 |  0 |  1 |  0 |  0 |  0 |  0 |
// | dest=M>>;jump         |  1 |  0 |  1 | 1 |  0 |  0 |  0 |  0 |  0 |  0 |
// Where:
// - "<<" is a left shift, and ">>" is a right shift, as defined in project 2.
//   These notations were chosen because they are used in real programming
//   languages.
// - dest and jump can take the same values as in the regular CPU.

CHIP CpuMul {
    IN  
        inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).
    OUT 
        outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction
    
    PARTS:
    Mux16(a=instruction, b=aluout, sel=instruction[15], out=mux1out);
    Not(in=instruction[15], out=notop);
    Or(a=notop, b=instruction[5], out=loada);
    ARegister(in=mux1out, load=loada, out[0..14]=addressM, out=regaout);
    Mux16(a=regaout, b=inM, sel=instruction[12], out=mux2out);
    And(a=instruction[15], b=instruction[4], out=loadd);
    And(a=instruction[15], b=instruction[3], out=writeM);
    DRegister(in=aluout, load=loadd, out=regdout);
    ExtendAlu(x=regdout, y=mux2out , instruction=instruction[6..14], out=outM, out=aluout, zr=tmpzr, ng=tmpng);
    And(a=instruction[2], b=tmpng, out=tmp1);
    And(a=instruction[1], b=tmpzr, out=tmp2);
    Not(in=tmpzr, out=ntmpzr);
    Not(in=tmpng, out=ntmpng);
    And(a=instruction[0], b=ntmpzr, out=tmp3);
    And(a=tmp3, b=ntmpng, out=tmp4);
    Or(a=tmp1, b=tmp2, out=or12);
    Or(a=or12, b=tmp4, out=or124);
    And(a=or124, b=instruction[15], out=loadpc);
    PC(in=regaout, load=loadpc, inc=true, reset=reset, out[0..14]=pc);
}
